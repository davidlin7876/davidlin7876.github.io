---
title: 股票算法-黄金坑
date: 
tags: [同花顺]
categories: 股票算法
description: 
top: 
---

# 包含的算法代码

1. 均线菱形战法，提示：试错，买点，减仓/补仓，清仓
1. 黄金坑战法，提示：底背离+均线多头分布
1. 布林线显示，提示：均线多头排列+突破布林上轨+突破短期震荡平台 

# 同花顺主图代码
![同花顺主图](http://static.flin.catxn.cn/%E9%BB%84%E9%87%91%E5%9D%91%E5%90%8C%E8%8A%B1%E9%A1%BA%E4%B8%BB%E5%9B%BE%E4%BB%A3%E7%A0%81.PNG)

|  参数  |  最小  |  最大  |  默认  |
| --- | --- | --- | --- |
|  N1  |  1  |  50  |  5  |
|  N2  |  1   |  100  |  11  |
|  N3  |  1  |  150  |  36  |
|  N4  |  1  |  50  |  25  |
|  SHORT  |  1  |  999  |  9  |
|  LONG  |  1  |  999  |  12  |
|  M  |  1  |  999  |  5  |
|  N  |  1  |  999  |  2  |
|  MM  |  1  |  999  |  10  |
|  NN  |  1  |  999  |  5  |

```
MA5:MA(CLOSE,N1),colorwhite,LINETHICK1;
MA11:MA(CLOSE,N2),colormagenta,LINETHICK1;
MA36:MA(CLOSE,N3),colorligreen,LINETHICK1;

FILLRGN(MAX(MA36,MIN(MA11,MA5)),MAX(MA36,MAX(MA11,MA5)),MA5<MA11 AND MA5>MA36,
color005500,MA5<MA11 AND MA11>MA36 AND MA5<MA36,colorgreen);
FILLRGN(MIN(MA5,MIN(MA11,MA36)),MIN(MA36,MA5),MA11<MA36 AND MA5>MA36,colorred,TRUE,color999900);
买点=MA11<MA36 AND MA5>MA36;
DRAWTEXT(买点 AND NOT(买点[1]),0.99*MIN(L,MA11),'买点'),colorred;
清仓点=MA5<MA11 AND MA11>MA36 AND MA5<MA36;
DRAWTEXT(清仓点 AND NOT(清仓点[1]),1.03*MAX(H,MA11),'清仓'),colorgreen;
试错点=MA5>MA11 AND MA5<MA36;
DRAWTEXT(试错点 AND NOT(试错点[1]),0.99*MIN(L,MA11),'试错'),colormagenta;
减仓点=MA5<MA11 AND MA5>MA36;
DRAWTEXT(减仓点 AND NOT(减仓点[1]),1.03*MAX(H,MA11),'减仓'),colorgreen;
加仓点=MA5>MA11 AND MA11>MA36 AND CROSS(MA5,MA11);
DRAWTEXT(加仓点,1.03*MAX(H,MA11),'补仓'),coloryellow;
减仓下=MA11<MA36 AND CROSS(MA11,MA5);
DRAWTEXT(减仓下,0.99*MIN(L,MA11),'减仓'),colorgreen;
//试错点后没有买点需要清仓,减仓点后面有清仓点需要补仓

DIFF:= EMA(CLOSE,SHORT) - EMA(CLOSE,LONG),colorcyan;
DEA:= EMA(DIFF,M),colorwhite;
gj:=MAX(C,O);
L4:=LLV(gj,4);
jl:=(HHV(diff,55)-LLV(diff,55))/5;
gt:=dea>REF(dea,1) AND REF(dea,1)<REF(dea,2);
A1:=BARSLAST(REF(gt,1));
底背离:=REF(L4,A1+1)>CLOSE AND diff>REF(diff,A1+1) AND gt;
DRAWTEXT(底背离,0.99*L,'底背离'),colormagenta;

均线多头分布=MA5>MA11 AND MA11>MA36;
黄金坑=REF(NOT(均线多头分布),1) AND 均线多头分布 AND COUNT(底背离,N4)>0;
DRAWGBK(黄金坑),color000077;
DRAWTEXT(黄金坑,0.99*L,"黄金坑"),colorwhite;

上轨:MA36+2*STD(CLOSE,36),colorred,DOTLINE;
下轨:MA36-2*STD(CLOSE,36),colorgreen,DOTLINE;

//均线多头排列
判断1:=MA5>=MA11 AND MA11>=MA36 AND MA36>=MA(C,200);
//突破布林上轨
判断2:=CROSS(C,上轨);
//突破短期震荡平台
判断3:=C>REF(HHV(H,NN),1) AND ABS(REF(HHV(H,NN),1)-REF(LLV(L,NN),1))/REF(LLV(L,NN),1)<MM/100;

DRAWGBK(判断1 AND 判断2 AND 判断3),coloryellow;
DRAWTEXT(判断1 AND 判断2 AND 判断3,0.99*L,"突破"),colorwhite;

```